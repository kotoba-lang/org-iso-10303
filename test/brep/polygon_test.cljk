(ns brep.polygon-test
  (:require [clojure.test :refer [deftest is testing]]
            [brep.polygon :as polygon]))

(defn- triangle-area [vertices [a b c]]
  (let [[[ax ay] [bx by] [cx cy]] (map #(nth vertices %) [a b c])]
    (/ (#?(:clj Math/abs :cljs js/Math.abs)
        (- (* (- bx ax) (- cy ay)) (* (- by ay) (- cx ax)))) 2.0)))

(defn- mesh-area [{:keys [vertices indices]}]
  (reduce + (map #(triangle-area vertices %) (partition 3 indices))))

(deftest triangulates-concave-ring
  (let [mesh (polygon/triangulate-rings [[[0 0] [4 0] [4 4] [2 2] [0 4]]])]
    (is (= 9 (count (:indices mesh))))
    (is (= 12.0 (mesh-area mesh)))))

(deftest triangulates-ring-with-hole
  (testing "the inner ring remains empty"
    (let [mesh (polygon/triangulate-rings
                [[[0 0] [6 0] [6 6] [0 6]]
                 [[2 2] [2 4] [4 4] [4 2]]])]
      (is (= 24 (count (:indices mesh))))
      (is (= 32.0 (mesh-area mesh))))))
