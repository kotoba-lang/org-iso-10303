(ns brep.mesh-csg-test
  (:require [clojure.test :refer [deftest is testing]]
            [brep.mesh-csg :as csg]))

(def cube-indices
  [0 2 1 0 3 2, 4 5 6 4 6 7,
   0 1 5 0 5 4, 3 7 6 3 6 2,
   0 4 7 0 7 3, 1 2 6 1 6 5])

(defn cube [[x y z] size]
  {:positions [[x y z] [(+ x size) y z] [(+ x size) (+ y size) z] [x (+ y size) z]
               [x y (+ z size)] [(+ x size) y (+ z size)]
               [(+ x size) (+ y size) (+ z size)] [x (+ y size) (+ z size)]]
   :indices cube-indices})

(defn mesh-volume [{:keys [positions indices]}]
  (#?(:clj Math/abs :cljs js/Math.abs)
   (/ (reduce + (map (fn [[ia ib ic]]
                       (let [[ax ay az] (nth positions ia)
                             [bx by bz] (nth positions ib)
                             [cx cy cz] (nth positions ic)]
                         (+ (* ax (- (* by cz) (* bz cy)))
                            (* ay (- (* bz cx) (* bx cz)))
                            (* az (- (* bx cy) (* by cx))))))
                     (partition 3 indices)))
      6.0)))

(deftest boolean-operations-on-overlapping-closed-meshes
  (let [a (cube [0.0 0.0 0.0] 2.0) b (cube [1.0 1.0 1.0] 2.0)]
    (doseq [[operation expected] [[:union 15.0] [:difference 7.0] [:intersection 1.0]]]
      (testing (name operation)
        (let [result (csg/mesh-boolean operation a b)]
          (is (seq (:indices result)))
          (is (< (#?(:clj Math/abs :cljs js/Math.abs)
                  (- expected (mesh-volume result)))
                 1.0e-6)))))))
